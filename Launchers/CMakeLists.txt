add_executable(AssignTraffic AssignTraffic.cc
                ../Algorithms/CHL/road_network.cpp ../Algorithms/CHL/util.cpp)
target_compile_definitions(AssignTraffic PRIVATE CSV_IO_NO_THREAD)
# target_include_directories(AssignTraffic PRIVATE ${Boost_INCLUDE_DIR})
target_link_libraries(AssignTraffic Boost::boost routingkit kassert vectorclass fast_cpp_csv_parser)
if((NUM_THREADS GREATER 1) AND OpenMP_FOUND)
  message("Linking openmp to AssignTraffic target.")
  target_link_libraries(AssignTraffic OpenMP::OpenMP_CXX)
  target_compile_definitions(AssignTraffic PRIVATE NUM_THREADS=${NUM_THREADS})
else()
  target_compile_definitions(AssignTraffic PRIVATE NUM_THREADS=1)
endif()

option(TA_USE_CFW "Make each descent direction conjugate to the last direction." ON)
option(TA_USE_SIMD_SEARCH "Use SIMD optimizations for the centralized shortest-path search." ON)
set(TA_LOG_K "" CACHE STRING "Choose the number of simultaneous shortest-path computations.")
set(TA_TTL_THETA "" CACHE STRING "Choose the maximum size of truncated subtrees int TruncatedTreeLabelling.")

if(NOT TA_USE_CFW)
  target_compile_definitions(AssignTraffic PRIVATE TA_NO_CFW)
endif()
if(NOT TA_USE_SIMD_SEARCH)
  target_compile_definitions(AssignTraffic PRIVATE TA_NO_SIMD_SEARCH)
endif()
if(NOT TA_LOG_K STREQUAL "")
  target_compile_definitions(AssignTraffic PRIVATE TA_LOG_K=${TA_LOG_K})
endif()
if(NOT TA_TTL_THETA STREQUAL "")
  target_compile_definitions(AssignTraffic PRIVATE TA_TTL_THETA=${TA_TTL_THETA})
endif()

add_executable(
    CalculateDemand CalculateDemand.cc
    ../External/randomc/mersenne.cpp ../External/randomc/userintf.cpp ../External/stocc/stoc1.cpp)
target_compile_definitions(CalculateDemand PRIVATE CSV_IO_NO_THREAD)
target_link_libraries(CalculateDemand ${PROJ_LIBRARY} ${RoutingKit_LIBRARY})
if(OpenMP_FOUND)
  target_link_libraries(CalculateDemand OpenMP::OpenMP_CXX)
endif()

set(DC_MAX_NUM_SOURCES "" CACHE STRING "Choose the number of sources to be substituted in FRAD.")
if(NOT DC_MAX_NUM_SOURCES STREQUAL "")
  target_compile_definitions(CalculateDemand PRIVATE DC_MAX_NUM_SOURCES=${DC_MAX_NUM_SOURCES})
endif()

add_executable(RunP2PAlgo RunP2PAlgo.cc)
target_compile_definitions(RunP2PAlgo PRIVATE CSV_IO_NO_THREAD)
target_link_libraries(RunP2PAlgo routingkit kassert vectorclass fast_cpp_csv_parser)
if(OpenMP_FOUND)
  target_link_libraries(RunP2PAlgo OpenMP::OpenMP_CXX)
endif()

option(USE_FAST_ELIMINATION_TREE_QUERY "Use the fast elimination tree query." ON)
if(NOT USE_FAST_ELIMINATION_TREE_QUERY)
  target_compile_definitions(RunP2PAlgo PRIVATE NO_FAST_ELIMINATION_TREE_QUERY)
endif()

add_executable(RunPoiAlgo RunPoiAlgo.cc)
target_link_libraries(RunPoiAlgo ${RoutingKit_LIBRARY})
if(OpenMP_FOUND)
  target_link_libraries(RunPoiAlgo OpenMP::OpenMP_CXX)
endif()
